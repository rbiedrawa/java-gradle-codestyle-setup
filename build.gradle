buildscript {
	dependencies {
		classpath 'com.diffplug.spotless:spotless-plugin-gradle:5.7.0'
		classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.17.0'
	}
}

plugins {
	id 'org.springframework.boot' version '2.6.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'com.diffplug.spotless' version '5.14.0'
}

group = 'com.rbiedrawa.spotless'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	systemProperty 'spring.profiles.active','test'
	useJUnitPlatform()
}

spotless {
	java {
		target 'src/main/java/**/*.java', 'src/main/test/**/*.java'
		toggleOffOn()  // enable spotless:off and spotless:on
		removeUnusedImports()
		trimTrailingWhitespace()
		palantirJavaFormat()

		importOrder('\\#', 'com.rbiedrawa', 'java', '')
	}
	groovyGradle {
		target '*.gradle'
		greclipse()
	}
	json {
		target '**/*.json'
		simple()
		eclipseWtp('json')
		targetExclude '.idea/**/*.json', 'out/**/*.json'
	}
	format 'misc', {
		target '**/.gitignore', '**/*.gradle', '**/*.md', '**/*.sh'
		indentWithTabs()
		trimTrailingWhitespace()
		endWithNewline()
	}
}
